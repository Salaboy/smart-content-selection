package com.wordpress.salaboy.content.events;

import com.wordpress.salaboy.content.model.*;
import com.wordpress.salaboy.content.model.components.*;
import com.wordpress.salaboy.content.model.meta.*;
import com.wordpress.salaboy.content.model.events.*;
import javax.swing.JTextArea;
import javax.swing.JDesktopPane;
import java.util.List;
import java.util.ArrayList;

global com.wordpress.salaboy.content.ui.Notifiable notifications;
global javax.swing.JDesktopPane panel;

declare ProductFocusGainedEvent
    @role( event )
end
declare VisitThresholdReachedEvent
    @role( event )
end

declare VisitComboEvent
    @role( event )
end
declare BuyProductEvent
    @role( event )
end

declare ProductFocusLostEvent
    @role( event )
end

declare SearchTriggeredEvent
    @role( event )
end


rule " Three visits product 1, tag it"
   when

      $list: List( $n: size == 3 ) from accumulate( $visit: ProductFocusGainedEvent(product.category == "Product 1") 
                                                            over window:time(5m) from entry-point "visit-product-stream",
                                                            collectList($visit)
                                                 )
   then
       notifications.addNotification("Three  Product 1 visits!!!");
       kcontext.getKnowledgeRuntime().getWorkingMemoryEntryPoint("buy-product-stream")
                        .insert(new VisitThresholdReachedEvent($n, ((ProductFocusGainedEvent)$list.get(0)).getProduct()));
       
end

rule " Four visits for product 2, tag it"
   when

      $list: List( $n: size == 4 ) from accumulate( $visit: ProductFocusGainedEvent(product.category == "Product 2") 
                                                            over window:time(5m) from entry-point "visit-product-stream",
                                                            collectList($visit)
                                                 )
   then
       notifications.addNotification("Four Product 2 visits!!!");
       kcontext.getKnowledgeRuntime().getWorkingMemoryEntryPoint("buy-product-stream")
                        .insert(new VisitThresholdReachedEvent($n, ((ProductFocusGainedEvent)$list.get(0)).getProduct()));
       
end

rule " Two visits for product 3, tag it"
   when

      $list: List( $n: size == 2 ) from accumulate( $visit: ProductFocusGainedEvent(product.category == "Product 3") 
                                                            over window:time(5m) from entry-point "visit-product-stream",
                                                            collectList($visit)
                                                 )
   then
       notifications.addNotification("Two Product 3 visits!!!");
       kcontext.getKnowledgeRuntime().getWorkingMemoryEntryPoint("buy-product-stream")
                        .insert(new VisitThresholdReachedEvent($n, ((ProductFocusGainedEvent)$list.get(0)).getProduct()));
       
end

rule " Focus gained without focus lost 10s"
   when

       $focusGained: ProductFocusGainedEvent(product.category == "Product 1")  from entry-point "visit-product-stream"
       not( ProductFocusLostEvent(product.category == "Product 1", this  after [0s,10s] $focusGained)  from entry-point "visit-product-stream")
                                                            
   then
       notifications.addNotification("Are You Interested in This Product -> Find out More!");
       
       
end


rule " Visits for product 1 followed by 3 and then 2 in two minutes"
   when

       $product1: ProductFocusGainedEvent(product.category == "Product 1") from entry-point "visit-product-stream"
       $product3: ProductFocusGainedEvent(product.category == "Product 3", this after [0s,2s] $product1) from entry-point "visit-product-stream"
       $product2: ProductFocusGainedEvent(product.category == "Product 2", this after [0s,2s] $product2, this after [0s,4s]  $product1 ) 
                                                                                            from entry-point "visit-product-stream"
                                                 
   then
       notifications.addNotification("1, 3, 2 Combo!");
       List products = new ArrayList();
       products.add($product1);
       products.add($product2);
       products.add($product3);

       kcontext.getKnowledgeRuntime().getWorkingMemoryEntryPoint("buy-product-stream")
                        .insert(new VisitComboEvent(products));
       
end

rule "when we get a  visit combo, insert the products for suggestions"
    when
       $combo: VisitComboEvent() from entry-point "buy-product-stream"
    then
       notifications.addNotification("Products in the combo: "+$combo.getProducts().size()); 
      
       SwingVisualComponent product4Component = new SwingVisualComponent("You are may be interested in: Product 4", 280, 200);
       insert(product4Component);
       panel.add(product4Component.getFrame(), javax.swing.JLayeredPane.DEFAULT_LAYER);
end





rule "No Buy after Threshold Reached! "
   when

      $thresholdReached: VisitThresholdReachedEvent($amount: amount) from entry-point "buy-product-stream"
      not( BuyProductEvent(this after[0s,10s] $thresholdReached)  )                                           
    then
       notifications.addNotification("No Buy :( after 10s but VisitThreshold Reached ("+$amount+")!");
       SwingVisualComponent bannerComponent = new SwingVisualComponent("Buy now 10% Discount on \n Product 1", 30, 200);
       insert(bannerComponent);
       panel.add(bannerComponent.getFrame(), javax.swing.JLayeredPane.DEFAULT_LAYER);
end

rule "Quickly Buy after Threshold Reached! "
   when
      $thresholdReached: VisitThresholdReachedEvent($amount: amount) from entry-point "buy-product-stream"
      BuyProductEvent(this after[0s,10s] $thresholdReached) from entry-point "buy-product-stream"                                          
    then
       notifications.addNotification("Thanks for buying so quickly!!! :) ");
       
end


//Based on focus sliding windows, create time based hot-topic lists

//Based on based on links and time windows create a list of interest / hot-topics and discard non interesting ones

//Filters based on categories and time between filters

// User Mode : General Search, Read Information, Looking for something in particular

//Interest Search: textbox for describe interest terms, we will look for metadata into the different components available and we will suggest filters
// that can lead to relevant products

//Create situation that requires a business process for a campaign creation






